! -----------------------------------------------------------------------------
! Office building
! We actually might not know that we're in an executive office? I'll call it that anyway for now.
! -----------------------------------------------------------------------------
Object OfficeDoor "door" with
    name
        'door',
    with_key KeyCard,
    before [;
        Open:
            if (KeyCard in player) rfalse;
            if (player in DirtRoad)
                KeyCard.cant_go();
            print "The door emits a steady beep, and the light turns green.";
            rfalse;
    ],
    when_closed
        "A steady red light glows from above the door's handle.",
    found_in
        DirtRoad OfficeEntry,
    door_to [;
        if (player in DirtRoad)
            return OfficeEntry;
        return OfficeSpace;
    ],
    door_dir [;
        if (player in DirtRoad)
            return n_to;
        return e_to;
    ],
has static door openable lockable locked;


Room OfficeEntry "Office Building" with
    name
        'building' 'office',
    description [;
        give OfficeDoor ~open;
        "You are in the main entrance of an office building. It looks like no one's been here in a while. The office door is to the east. You can also go back south.";
    ],
    s_to DirtRoad,
    e_to OfficeDoor;


Room OfficeSpace "Main Office Floor" with
    description
        "You are in the main area of an office building.^^
        A door to the west leads back to the entryway. There is a door to the north, and a set of stairs leads to the second floor.",
    w_to OfficeEntry,
    n_to PrinterRoom,
    u_to ExecOffice;


Room PrinterRoom "Printer Room" with
    description
        "You are in a printer room.^^
        The main office space is to the south of you.",
        s_to OfficeSpace;

Object -> Printer "dot matrix printer" with
    name
        'dot' 'matrix' 'printer',
    initial [;
        print "A dusty dot matrix printer sits in the corner, forlorn.";
        if (Peacock in PrinterRoom)
            "^It seems to be making a curious noise, however.";
        rtrue;
    ],
    description
        "The printer looks like it hasn't been used in roughly thirty-nine years... give or take.",
    before [;
        Examine:    Peacock.initial(); !*boss fight music begins*
    ],
has static;

! need to declare the printout after the peacock has been defeated
Object -> -> Paper "paper printout" with
    description
        "There's a paper printout here.",
has scenery;


! cheating a little so I don't have to write a hallway until I'm ready to expand this.
! exec office init plays automagically because teleporting skips it
Scene StairScene with
    initial [;
        print "Up the stairs, you find yourself in a long hallway. It doesn't take you long, however, to find the room number written on the paper.";
        Any();
        print "The door opens with your keycard -- maybe this is the owner's office?";
        Any();
        ExecOffice.initial();
        move player to ExecOffice;
    ],
has static light;

Room ExecOffice "Executive Office" with
    initial
        "Well, whoever's office this is, they're probably lower management -- it's nice, but not ostentatious.",
    description
        "You are in an executive office.",
    d_to OfficeSpace;

Object -> Terminal "terminal" with
    name
        'terminal' 'term',
    initial
        "An old computer terminal sits on the desk near the window. It appears to be on, giving off a soft light from the screen.",
    description
        "It's an old computer terminal.",
has static light;



Room ServerRoom "Server Room" with
    description
        "You are in a fairly large server room.",
has ~light;